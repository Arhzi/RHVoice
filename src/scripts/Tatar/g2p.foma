# Copyright (C) 2017  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

read spaced-text russian-loanwords.txt
define RussianDict0
clear

define RussianVTranscription в -> v ; 

define RussianDict RussianDict0 .o. RussianVTranscription ; 

define TatarVowelLetter а|ә|е|ё|и|о|ө|у|ү|ы|э|ю|я ; 
define TatarConsonantLetter б|в|г|д|ж|җ|з|й|к|л|м|н|ң|п|р|с|т|ф|х|һ|ц|ч|ш|щ|ğ|q|w ; 
define TatarLetter TatarVowelLetter | TatarConsonantLetter | ь | ъ ; 

define RussianStem RussianDict.u ; 
define Prefix(x) [x .o. [x [TatarLetter+]:0]].l ; 
define RussianPrefixStem1 Prefix(RussianStem) ; 
define RussianStem1 RussianStem - RussianPrefixStem1 ; 
define RussianLoan1 [RussianStem1 .o. RussianDict] TatarLetter* ; 
define RussianPrefixStem2 Prefix(RussianPrefixStem1) ; 
define RussianStem2 RussianPrefixStem1 - RussianPrefixStem2 ; 
define RussianLoan2 [RussianStem2 .o. RussianDict] TatarLetter* ; 
define RussianPrefixStem3 Prefix(RussianPrefixStem2) ; 
define RussianStem3 RussianPrefixStem2 - RussianPrefixStem3 ; 
define RussianLoan3 [RussianStem3 .o. RussianDict] TatarLetter* ; 
define RussianPrefixStem4 Prefix(RussianPrefixStem3) ; 
define RussianStem4 RussianPrefixStem3 - RussianPrefixStem4 ; 
define RussianLoan4 [RussianStem4 .o. RussianDict] TatarLetter* ; 
define RussianPrefixStem5 Prefix(RussianPrefixStem4) ; 
define RussianStem5 RussianPrefixStem4 - RussianPrefixStem5 ; 
define RussianLoan5 [RussianStem5 .o. RussianDict] TatarLetter* ; 
define RussianPrefixStem6 Prefix(RussianPrefixStem5) ; 
define RussianStem6 RussianPrefixStem5 - RussianPrefixStem6 ; 
define RussianLoan6 [RussianStem6 .o. RussianDict] TatarLetter* ; 

define RussianLoan RussianLoan1 .P. RussianLoan2 .P. RussianLoan3 .P. RussianLoan4 .P. RussianLoan5 .P. RussianLoan6 ; 

define LatinVowelLetter a|e|i|o|u ; 
define LatinConsonantLetter b|v|w|g|ğ|d|j|c|z|y|k|q|l|m|n|p|r|s|t|f|x|h ; 
define LatinLetter LatinVowelLetter | LatinConsonantLetter ; 

define TatarWord RussianLoan .P. [[TatarLetter|LatinLetter]+] ; 

define TatarConsonant b|v|g|d|zh|zhj|z|j|k|l|m|n|ng|p|r|s|t|f|hh|h|shj|sh|w ; 
define TatarVowel a|af|e|i|y|o|of|u|uf ; 

define TatarBasicConsonantTranscription б:b|в:w|г:g|д:d|[ж|j]:zh|җ:zhj|з:z|й:j|к:k|л:l|м:m|н:n|ң:ng|п:p|р:r|с:s|т:t|ф:f|х:hh|һ:h|ц:c|ч:shj|ш:sh|щ:shj|ğ:gh|q:kh|w:w|x:{ks}|c:{ts} ; 
define TatarBasicConsonantsTranscription TatarBasicConsonantTranscription -> ; 

define TatarIntervocalicCTranscription c -> {ts} || TatarVowelLetter _ TatarVowelLetter ; 
define TatarDefaultCTranscription c -> s ; 

define TatarBasicVowelTranscription а:a|ә:af|е:je|ё:jo|и:i|о:o|ө:of|у:u|ү:uf|ы:y|э:e|ю:ju|я:ja|y:i ; 
define TatarBasicVowelsTranscription TatarBasicVowelTranscription -> ; 

define TatarJVowelsAfterConsonants je:e|ju:uf|ja:af|ё:of -> || TatarConsonant _ ; 

define TatarJInsertion [..] -> j || _ je|jo|ju|ja ; 

define TatarFrontVowel af|e|i|of|uf ; 
define TatarBackVowel a|o|u|y ; 

define TatarJeAsBackTranscription 
je -> y \\ _ [\TatarFrontVowel]* TatarBackVowel ; 

define TatarJVowelsAsFrontTranscription 
[ju:uf|ja:af] -> \\ _ [\TatarBackVowel]* TatarFrontVowel ; 

define TatarJVowelsBeforeSoftSignTranscription 
ja:af|ju:uf -> || .#. j _ TatarConsonant+ ь .#. ; 

define TatarJVowelsDefaultTranscription 
ja:a|je:e|ju:u|jo:o -> ; 

define TatarUvularStopsTranscription 
k:kh|g:gh -> || _ ъ|TatarBackVowel , TatarBackVowel _ TatarConsonant+ TatarBackVowel | .#. ; 

define TatarSoftAndHardSignsRemoval ь|ъ -> 0 || _ ; 

define TatarNonSyllabicUToW 
[u|uf] -> w || TatarVowel _ ; 

define TatarTranscription 
TatarWord .o. 
TatarBasicConsonantsTranscription .o. 
TatarIntervocalicCTranscription .o. 
TatarDefaultCTranscription .o. 
TatarBasicVowelsTranscription .o. 
TatarJVowelsAfterConsonants .o. 
TatarJInsertion .o. 
TatarJeAsBackTranscription .o. 
TatarJVowelsAsFrontTranscription .o. 
TatarJVowelsBeforeSoftSignTranscription .o. 
TatarJVowelsDefaultTranscription .o. 
TatarUvularStopsTranscription .o. 
TatarNonSyllabicUToW .o. 
TatarSoftAndHardSignsRemoval ; 

define TatarBasicPromptConsonantTranscription  [в:v] .P. TatarBasicConsonantTranscription ; 
define TatarBasicPromptConsonantsTranscription TatarBasicPromptConsonantTranscription -> ; 

define TatarPromptTranscription 
[TatarLetter+] .o. 
TatarBasicPromptConsonantsTranscription .o. 
TatarIntervocalicCTranscription .o. 
TatarDefaultCTranscription .o. 
TatarBasicVowelsTranscription .o. 
TatarJVowelsAfterConsonants .o. 
TatarJInsertion .o. 
TatarJVowelsAsFrontTranscription .o. 
TatarJVowelsBeforeSoftSignTranscription .o. 
TatarJVowelsDefaultTranscription .o. 
TatarSoftAndHardSignsRemoval ; 

regex TatarTranscription ; 
