# Copyright (C) 2012, 2014  Olga Yakovleva <yakovleva.o.v@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

define NZDigit 1|2|3|4|5|6|7|8|9;
define Digit %0|NZDigit;
define PlusOrMinus %+|%-;
define Number (PlusOrMinus) [%0|[NZDigit Digit^<15]];

define InsertThousands 
[..] -> thousand || Digit _ Digit^3 .#.;

define InsertMillions 
[..] -> million || Digit _ Digit^3 thousand;

define InsertBillions 
[..] -> billion || Digit _ Digit^3 million;

define InsertTrillions 
[..] -> trillion || Digit _ Digit^3 billion;

define RemoveSkippedParts 
%0 %0 %0 [thousand|million|billion|trillion] -> 0 ;

define InsertHundreds 
[..] -> hundred || NZDigit _ Digit^2 ,, 
%0 -> 0 || _ Digit^2 ;

define Units %0:0|1:one|2:two|3:three|4:four|5:five|6:six|7:seven|8:eight|9:nine;

define Teens 1:0 [%0:ten|1:eleven|2:twelve|3:thirteen|4:fourteen|5:fifteen|6:sixteen|7:seventeen|8:eighteen|9:nineteen];

define Tens %0:0|2:twenty|3:thirty|4:forty|5:fifty|6:sixty|7:seventy|8:eighty|9:ninety;

define UpToHundred %0:zero .P. [Teens | [(Tens) Units]];

define DigitsToWords 
UpToHundred @-> ,, 
%+ @-> plus ,, 
%- @-> minus ; 

regex 
Number .o. 
InsertThousands .o. 
InsertMillions .o. 
InsertBillions .o. 
InsertTrillions .o. 
RemoveSkippedParts .o. 
InsertHundreds .o. 
DigitsToWords;
